1.) Two cache policies
    static (aggressive caching)
    api (caching disabled OR origin-driven)

- Static (aggressive caching):

resource "aws_cloudfront_cache_policy" "my_cache_static01" {
  name        = "lab-cache-static01"
  comment     = "Aggressive caching for /static/*"
  default_ttl = 86400    # 1 day
  max_ttl     = 31536000 # 1 year
  min_ttl     = 0

  parameters_in_cache_key_and_forwarded_to_origin {

    cookies_config { cookie_behavior = "none" }

    # Explanation: Static should not vary on query strings (unless you do versioning); students can change later.
    query_strings_config { query_string_behavior = "none" }

    # Explanation: Keep headers out of cache key to maximize hit ratio.
    headers_config { header_behavior = "none" }

    enable_accept_encoding_gzip   = true
    enable_accept_encoding_brotli = true
  }
}

- API caching disabled:

resource "aws_cloudfront_cache_policy" "my_cache_api_disabled01" {
  name        = "lab-cache-api-disabled01"
  comment     = "Caching disabled for API behavior"
  default_ttl = 0
  min_ttl     = 0
  max_ttl     = 0

  parameters_in_cache_key_and_forwarded_to_origin {
    # must be false when caching is disabled
    enable_accept_encoding_gzip   = false
    enable_accept_encoding_brotli = false

    headers_config {
      header_behavior = "none"
    }

    cookies_config {
      cookie_behavior = "none"
    }

    query_strings_config {
      query_string_behavior = "none"
    }
  }
}


2) Two origin request policies
    static minimal forwarding
    api forwards required headers/query/cookies (but doesn’t necessarily include them in cache key)

- Static minimal forwarding:

resource "aws_cloudfront_origin_request_policy" "my_orp_static01" {
  name    = "lab-orp-static01"
  comment = "Minimal forwarding for static assets"

  cookies_config { cookie_behavior = "none" }
  query_strings_config { query_string_behavior = "none" }
  headers_config { header_behavior = "none" }
}

- api forwards required headers/query/cookies:

resource "aws_cloudfront_origin_request_policy" "my_orp_api01" {
  name    = "lab-orp-api01"
  comment = "Forward necessary values for API calls"

  cookies_config { cookie_behavior = "all" }
  query_strings_config { query_string_behavior = "all" }

  headers_config {
    header_behavior = "whitelist"
    headers {
      items = ["Content-Type", "Origin", "Host"]
    }
  }
}

 3) Two cache behaviors
    path_pattern = "/static/*" → static policies
    path_pattern = "/api/*" → api policies

- path_pattern = "/static/*" -> static policies:

ordered_cache_behavior {
  path_pattern           = "/static/*"
  target_origin_id       = "lab-alb-origin01"
  viewer_protocol_policy = "redirect-to-https"

  allowed_methods = ["GET","HEAD","OPTIONS"]
  cached_methods  = ["GET","HEAD"]

  cache_policy_id            = aws_cloudfront_cache_policy.my_cache_static01.id
  origin_request_policy_id   = aws_cloudfront_origin_request_policy.my_orp_static01.id
  response_headers_policy_id = aws_cloudfront_response_headers_policy.my_rsp_static01.id
}


- path_pattern = "/api/*" → api policies
ordered_cache_behavior {
  path_pattern           = "/api/*"
  target_origin_id       = "lab-alb-origin01"
  viewer_protocol_policy = "redirect-to-https"

  allowed_methods = ["GET","HEAD","OPTIONS","PUT","POST","PATCH","DELETE"]
  cached_methods  = ["GET","HEAD"]

  cache_policy_id          = aws_cloudfront_cache_policy.my_cache_api_disabled01.id
  origin_request_policy_id = aws_cloudfront_origin_request_policy.my_orp_api01.id
}


 4) Be A Man Challenge: Response headers policy for explicit Cache-Control on static responses (or security headers):

 resource "aws_cloudfront_response_headers_policy" "my_rsp_static01" {
  name    = "lab-rsp-static01"
  comment = "Add explicit Cache-Control for static content"

  custom_headers_config {
    items {
      header   = "Cache-Control"
      override = true
      value    = "public, max-age=86400, immutable"
    }
  }
}